
buildscript {
  repositories {
    gradlePluginPortal()
  }
  dependencies {
    classpath "io.github.fourlastor:construo:2.1.0"
    if(enableGraalNative == 'true') {
      classpath "org.graalvm.buildtools.native:org.graalvm.buildtools.native.gradle.plugin:0.9.28"
    }
  }
}
plugins {
  id "application"
}
apply plugin: 'io.github.fourlastor.construo'


import io.github.fourlastor.construo.Target

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'com.ygames.ysoccer.lwjgl3.Lwjgl3Launcher'
application.setMainClass(mainClassName)
eclipse.project.name = appName + '-lwjgl3'
java.sourceCompatibility = 8
java.targetCompatibility = 8
if (JavaVersion.current().isJava9Compatible()) {
        compileJava.options.release.set(8)
}

dependencies {
  implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
  implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx-video:gdx-video-lwjgl:$gdxVideoVersion"
  implementation project(':core')

  if(enableGraalNative == 'true') {
    implementation "io.github.berstanio:gdx-svmhelper-backend-lwjgl3:$graalHelperVersion"
      implementation "io.github.berstanio:gdx-svmhelper-extension-box2d:$graalHelperVersion"
      implementation "io.github.berstanio:gdx-svmhelper-extension-freetype:$graalHelperVersion"
    }

}

def os = System.properties['os.name'].toLowerCase()

run {
  workingDir = rootProject.file('assets').path
// You can uncomment the next line if your IDE claims a build failure even when the app closed properly.
  //setIgnoreExitValue(true)

  if (os.contains('mac')) jvmArgs += "-XstartOnFirstThread"
}

jar {
// sets the name of the .jar file this produces to the name of the game or app, with the version after.
  archiveFileName.set("${appName}-${projectVersion}.jar")
// the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
// these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'music', 'docs', 'data', 'images', 'sounds')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
// setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
// this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

construo {
    // name of the executable
    name.set(appName)
    // human-readable name, used for example in the `.app` name for macOS
    humanName.set(appName)
    // Optional, defaults to project version property
    // version.set("$projectVersion")
  construo {
    jlink {
      // guess the modules from the jar using jdeps, defaults to true
      //guessModulesFromJar.set(false)
    }
  }

    targets.configure {
      create("linuxX64", Target.Linux) {
        architecture.set(Target.Architecture.X86_64)
        jdkUrl.set("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.12%2B7/OpenJDK17U-jdk_x64_linux_hotspot_17.0.12_7.tar.gz")
      }
      create("macM1", Target.MacOs) {
        architecture.set(Target.Architecture.AARCH64)
        jdkUrl.set("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.12%2B7/OpenJDK17U-jdk_aarch64_mac_hotspot_17.0.12_7.tar.gz")
        // macOS needs an identifier
        identifier.set("com.ygames.ysoccer." + appName)
        // Optional: icon for macOS
        macIcon.set(project.file("icons/logo.icns"))
      }
      create("macX64", Target.MacOs) {
        architecture.set(Target.Architecture.X86_64)
        jdkUrl.set("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.12%2B7/OpenJDK17U-jdk_x64_mac_hotspot_17.0.12_7.tar.gz")
        // macOS needs an identifier
        identifier.set("com.ygames.ysoccer." + appName)
        // Optional: icon for macOS
        macIcon.set(project.file("icons/logo.icns"))
      }
      create("winX64", Target.Windows) {
        architecture.set(Target.Architecture.X86_64)
        jdkUrl.set("https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.12%2B7/OpenJDK17U-jdk_x64_windows_hotspot_17.0.12_7.zip")
        // Uncomment the next line to show a console when the game runs, to print messages.
        useConsole.set(true)
        // Optional: icon for Windows
        icon.set(project.file("icons/logo.png"))
      }
    }
}

String javaUrl = "";

File buildDirResolved = project.layout.buildDirectory.get().asFile
File tmpDir = new File(buildDirResolved, "tmp")
File outputDir = new File(buildDirResolved, "libs")

tasks.register('downloadAndUnzipJava') {
  doLast {

    if (!javaUrl || javaUrl.isEmpty()) {
      throw new GradleException("javaUrl no está definido")
    }

    tmpDir.mkdirs()
    outputDir.mkdirs()

    // Detectar extensión
    boolean isTarGz = javaUrl.endsWith(".tar.gz") || javaUrl.endsWith(".tgz")
    boolean isZip = javaUrl.endsWith(".zip")

    if (!isZip && !isTarGz) {
      throw new GradleException("Formato no soportado: $javaUrl")
    }

    // Archivo destino
    File archiveFile = new File(
      tmpDir,
      isZip ? "archivo.zip" : "archivo.tar.gz"
    )

    // Descargar
    new URL(javaUrl).withInputStream { input ->
      archiveFile.withOutputStream { output ->
        output << input
      }
    }

    // Descomprimir
    project.copy {
      if (isZip) {
        from project.zipTree(archiveFile)
      } else {
        from project.tarTree(project.resources.gzip(archiveFile))
      }
      into outputDir
    }
  }
}

tasks.register('laffCopyDist', Copy) {
  dependsOn clean
  dependsOn jar
  dependsOn downloadAndUnzipJava

  def assetsPath = rootProject.file('assets')
  def libDir = layout.buildDirectory.dir("libs")

  from(assetsPath) {
    include 'music/**'
    include 'data/**'
    include 'images/**'
    include 'sounds/**'
    from(new File(assetsPath, "docs")) {
      include '**/*'
      into ''
    }
  }

  into(libDir)
}

tasks.register('laffDistWindows') {
  javaUrl = "https://corretto.aws/downloads/latest/amazon-corretto-8-x64-windows-jre.zip"
  dependsOn laffCopyDist

  doLast {
    File libDir = project.layout.buildDirectory.get().asFile
    File cmdFile = new File(outputDir, "LaffSoccer.cmd")

    cmdFile.text = """@echo off
jre8\\bin\\java.exe -jar ${appName}-${projectVersion}.jar
"""
  }
}

tasks.register('laffDistLinux') {
  javaUrl = "https://corretto.aws/downloads/latest/amazon-corretto-8-x64-linux-jdk.tar.gz"
  dependsOn laffCopyDist

  doLast {
    File libDir = project.layout.buildDirectory.get().asFile
    File cmdFile = new File(outputDir, "LaffSoccer.sh")

    cmdFile.text = """#!/bin/sh
amazon-corretto-8.472.08.1-linux-x64/bin/java -jar ${appName}-${projectVersion}.jar
"""
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
tasks.register('dist') {
  dependsOn 'jar'
}

distributions {
  main {
    contents {
      into('libs') {
        project.configurations.runtimeClasspath.files.findAll { file ->
          file.getName() != project.tasks.jar.outputs.files.singleFile.name
        }.each { file ->
          exclude file.name
        }
      }
    }
  }
}

startScripts.dependsOn(':lwjgl3:jar')
startScripts.classpath = project.tasks.jar.outputs.files

if(enableGraalNative == 'true') {
  apply from: file("nativeimage.gradle")
}

